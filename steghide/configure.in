dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/main.cc)
AC_CANONICAL_SYSTEM
AM_INIT_AUTOMAKE(steghide, 0.5.1)
AM_CONFIG_HEADER(config.h)

dnl checks for programs.
AC_PROG_CXX
AC_PROG_INSTALL
AC_PROG_AWK
AC_PROG_LN_S

dnl GNU gettext
ALL_LINGUAS="de fr"
AC_CHECK_FUNCS(strchr)
AM_GNU_GETTEXT

dnl check if debugging support is requested
AC_MSG_CHECKING([wether to enabled -DDEBUG])
AC_ARG_ENABLE(debug,[  --enable-debug          enable code used only for debugging],
	if test "$enableval" = yes ;
	then
		AC_MSG_RESULT([yes])
		AC_DEFINE(DEBUG,1,[enable code used only for debugging])
	else
		AC_MSG_RESULT([no])
	fi
)

dnl Check for libmhash
AC_CHECK_LIB(mhash, mhash_init)
AC_CHECK_HEADER(mhash.h)
if test "$ac_cv_header_mhash_h" = no || test "$ac_cv_lib_mhash_mhash_init" = no ;
then
	echo "**********";
	echo "libmhash could not be found on your system but is needed to compile steghide.";
	echo "libmhash can be downloaded from http://mhash.sourceforge.net/.";
	echo "**********";
	AC_MSG_ERROR([[libmhash not found]])
fi

dnl Check for libmcrypt
AC_CHECK_LIB(dld, dld_init)
AC_CHECK_LIB(ltdl, lt_dlinit)
AC_CHECK_LIB(mcrypt, mcrypt_module_open, , , -lltdl)
AC_CHECK_HEADER(mcrypt.h)
if test "$ac_cv_header_mcrypt_h" = no || test "$ac_cv_lib_mcrypt_mcrypt_module_open" = no ;
then
	echo "**********";
	echo "libmcrypt could not be found on your system. Steghide will be compiled without";
	echo "support for encryption. You will not be able to extract encrypted data!";
	echo "libmcrypt can be downloaded from http://mcrypt.hellug.gr/";
	echo "**********";
else
	AC_DEFINE(USE_LIBMCRYPT,1,[use the libmcrypt library (header and lib are present)])
fi	

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(limits.h termios.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

dnl Check size of unsigned types
AC_CHECK_SIZEOF(unsigned long int)
AC_CHECK_SIZEOF(unsigned int)
AC_CHECK_SIZEOF(unsigned short int)
AC_CHECK_SIZEOF(unsigned char)

if test "$ac_cv_sizeof_unsigned_int" = 4;
then
	AC_DEFINE(TYPE_UWORD32,[unsigned int],[type of unsigned word with 32 bits])
else
if test "$ac_cv_sizeof_unsigned_long_int" = 4;
then
	AC_DEFINE(TYPE_UWORD32,[unsigned long int],[type of unsigned word with 32 bits])
else
	AC_MSG_ERROR([[There is no unsigned integer type with a size of 32 bits on your system. Cannot compile.]])
fi
fi	

if test "$ac_cv_sizeof_unsigned_short_int" = 2;
then
	AC_DEFINE(TYPE_UWORD16,[unsigned short int],[type of unsigned word with 16 bits])
else
if test "$ac_cv_sizeof_unsigned_int" = 2;
then
	AC_DEFINE(TYPE_UWORD16,[unsigned int],[type of unsigned word with 16 bits])
else
	AC_MSG_ERROR([[There is no unsigned integer type with a size of 16 bits on your system. Cannot compile.]])
fi
fi	

if test "$ac_cv_sizeof_unsigned_char" = 1;
then
	AC_DEFINE(TYPE_BYTE,[unsigned char],[type of unsigned byte])
else
	AC_MSG_ERROR([[There is no unsigned character type with a size of 8 bits on your system. Cannot compile.]])
fi	

dnl check for doxygen
AC_CHECK_PROG(ac_cv_prog_doxygen, "doxygen", yes, no)
AM_CONDITIONAL(HAVE_DOXYGEN, test "$ac_cv_prog_doxygen" = yes)

dnl check for perl
AC_CHECK_PROG(ac_cv_prog_perl, "perl", yes, no)
AM_CONDITIONAL(HAVE_PERL, test "$ac_cv_prog_perl" = yes)

dnl Check for random sources	
AC_CHECK_FILE(/dev/urandom, [AC_DEFINE(HAVE_DEV_URANDOM,1,/dev/urandom is available)])
AC_CHECK_FILE(/dev/random, [AC_DEFINE(HAVE_DEV_RANDOM,1,/dev/random is available)])

dnl Checks for library functions.
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(tcgetattr tcsetattr strtoul)

dnl system-specific settings
case "${target}" in
	*-cygwin*)
	AC_DEFINE(WIN32,1,target system is win32)
	LIBS="${LIBS} -lintl"
	;;
esac

dnl check if needed/recommended libraries are present
AC_OUTPUT([Makefile doc/Makefile intl/Makefile po/Makefile.in src/Makefile tests/Makefile])




